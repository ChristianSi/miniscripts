#!/usr/bin/perl -CA

use 5.014;
use open qw(:std :utf8);  # use UTF-8 for all I/O (by default)
use utf8;                 # allow UTF-8 in source code
use warnings;

use File::Copy 'copy';
use Archive::Zip ':ERROR_CODES';

# fixepubchars $filename:
sub fixepubchars {

    my ($filename) = @_;
    my $zip = Archive::Zip->new();
    $zip->read($filename) == AZ_OK or die "Error reading $filename\n";
    my $any_changed = 0;

    for my $htmlMember ($zip->membersMatching('.*\.x?html?')) {
        my $html = $zip->contents($htmlMember);
        utf8::decode($html) or die "Cannot decode contents of ${\$htmlMember->fileName()} as UTF-8";

        my $changed = $html =~
            tr/ᴀʙᴄᴅᴇꜰɢʜɪᴊᴋʟᴍɴᴏᴘʀꜱᴛᴜᴠᴡʏᴢ/0123456789ABCDEFGHIJKLMNOPRSTUVWYZABCDEFIMNOPRSTUVZ/;

        if ($changed) {
            utf8::encode($html);
            $zip->contents($htmlMember, $html);
            $any_changed |= 1;
        }
    }

    if ($any_changed) {
        copy $filename, "$filename.orig";
        $zip->overwrite() == AZ_OK or die "Error writing $filename\n";
        warn "fixepubchars: $filename has been fixed\n";
    } else {
        warn "fixepubchars: $filename hasn't changed\n";
    }
}

sub show_usage {
    die <<'END';
Usage: fixepubchars EPUB_FILE

Fixes EPUB_FILE by replacing any small caps and old-style numerals with
their ASCII equivalents.

If any changes to EPUB_FILE are necessary, the original file will be
retained as EPUB_FILE.orig.
END
}

if (@ARGV == 1) {
    fixepubchars $ARGV[0];
} else {
    show_usage;
}

=head1 NAME

fixepubchars -- Fixes epubs by replacing any small caps and old-style numerals with their ASCII
equivalents

=head1 SYNOPSIS

    fixepubchars EPUB_FILE

If any changes to EPUB_FILE are necessary, the original file will be retained as EPUB_FILE.orig.

=head1 PREREQUISITES

L<Archive::Zip>

=head1 AUTHOR

Christian Siefkes <christian@siefkes.net>

=head1 COPYRIGHT

Copyright (c) 2016 Christian Siefkes

Permission to use, copy, modify, and/or distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY
AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR
OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
PERFORMANCE OF THIS SOFTWARE.
